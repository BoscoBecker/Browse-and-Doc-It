'
' This function returns the supplied string with the parameters inserted into
' the string inplace of the %param% markers.
'
' @Version 1.0
' @Date    14 Aug 2003
'
' @precon  None.
' @postcon Returns the strText with the %param% place holders replaced with the
'          item in the Params array. If there are too many params an error is
'          raised.
'
' @param   strText as a String
' @param   Params as an Variant Array.
' @return  a String
'
Public Function FormatString(strText As String, ParamArray Params()) As String
  Const strParam = "%param%"
  Const iLen = 7
  Dim i As Long
  Dim iPos As Long
  Dim str As String
  Exception.Push "PrivateFunctions.FormatString", strText, Params()
  On Error GoTo ErrHnd
  str = strText
  For i = LBound(Params) To UBound(Params)
    iPos = InStr(str, strParam)
    If iPos <> 0 Then
      str = Left$(str, iPos - 1) & Params(i) & _
        Right$(str, Len(str) - iPos - iLen + 1)
    Else
      Err.Raise 1000, "FormatString()", "Parameter not found in source string."
    End If
  Next i
  FormatString = str
ErrHnd:
  If Err.Number <> 0 Then Exception.DisplayErrorMessage Err
  Exception.Pop
End Function
