'
' This method returns the value of the field defined within the comma
' delimited string.
'
' @Version 1.0
' @Date    14 Aug 2003
'
' @precon  None.
' @postcon Returns the indexed field with in the delimited line of values.
'
' @param   strText as a String
' @param   iField as a Long
' @param   iDelimiter as a Long
' @return  a String
'
Public Function GetField(strText As String, iField As Long, _
  iDelimiter As Long) As String
  Dim iSt As Long
  Dim iEd As Long
  Dim iCount As Long
  Dim i As Long
  Exception.Push "PrivateFunctions.GetField", strText, iField, iDelimiter
  On Error GoTo ErrHnd
  iCount = 1 + CharCount(strText, iDelimiter)
  ' Find iSt
  If iField < 1 Then Err.Raise 9999, , "Field Error"
  If iField = 1 Then iSt = 0
  If iField > 1 And iField <= iCount Then
    i = 1
    For iSt = 1 To Len(strText)
      If Mid$(strText, iSt, 1) = Chr$(iDelimiter) Then i = i + 1
      If i = iField Then Exit For
    Next iSt
  End If
  If iField > iCount Then Err.Raise 9999, , "Field Error"
  ' Find iEd
  If iField < 1 Then Err.Raise 9999, , "Field Error"
  If iField = iCount Then iEd = Len(strText) + 1
  If iField >= 1 And iField < iCount Then
    i = 0
    For iEd = 1 To Len(strText)
      If Mid$(strText, iEd, 1) = Chr$(iDelimiter) Then i = i + 1
      If i = iField Then Exit For
    Next iEd
  End If
  If iField > iCount Then Err.Raise 9999, , "Field Error"
  GetField = Mid$(strText, iSt + 1, iEd - iSt - 1)
ErrHnd:
  If Err.Number <> 0 Then Exception.DisplayErrorMessage Err
  Exception.Pop
End Function
