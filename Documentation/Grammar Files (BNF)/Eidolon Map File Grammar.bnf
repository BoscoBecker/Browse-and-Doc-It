/**

  Browse and Doc It Eidolon Map Files Grammar

  @Author  David Hoyle
  @Date    12 Nov 2012
  @Version 1.0

  @usesemicolon

**/
<Goal>                    ::= ( <TextTable> |
                                <DBTable> |
                                <TimeLocationTable> |
                                <OutputTable> |
                                <RequirementsTable> )*;

<TextTable>               ::= <DefinitionName> '=' 'CLASS' '(' 'TEXTTABLE' ')' <LineEnd>
                              '{' <LineEnd>
                              [ <TextTableDef> ]
                              <FieldDef>+
                              '}' <LineEnd>;

<DBTable>                 ::= <DefinitionName> '=' 'CLASS' '(' 'DBTABLE' ')' <LineEnd>
                             '{' <LineEnd>
                             [ <PrimaryConnection> ]
                             <FieldDef>+
                             '}' <LineEnd>;

<TimeLocationTable>       ::= <DefinitionName> '=' 'CLASS' '(' 'TIMELOCATIONTABLE' ')' <LineEnd>
                             '{' <LineEnd>
                             <FieldDef>+
                             <TimeLocationDef>+
                             '}' <LineEnd>;

<OutputTable>             ::= <DefinitionName> '=' 'CLASS' '(' 'OUTPUTTABLE' ')' <LineEnd>
                              '{' <LineEnd>
                              [ <PrimaryConnection> ]
                              [ <SecondaryConnection> ]
                              <FieldDef>+
                              '}' <LineEnd>;

<RequirementsTable>       ::= <DefinitionName> '=' 'CLASS' '(' 'REQUIREMENTSTABLE' ')' <LineEnd>
                              '{' <LineEnd>
                              [ <PrimaryConnection> ]
                              <FieldDef>+
                              <AssociationDef>+
                              <FieldDef>+
                              <AssociationDef>+
                              '}' <LineEnd>;

<PrimaryConnection>       ::= <DatabaseDef> <ConnectionDef> <TableNameDef>;

<SecondaryConnection>     ::= <PrimaryConnection>;

<TextTableDef>            ::= '#TABLENAME' '=' <FileName> <LineEnd>;

/** Use <FileName> for M  S Access database with a NULL <Connectiondef>, else use <Directory>
    and a non-NULL <Conn  ectionDef>. **/
<DatabaseDef>             ::= '#DATABASE' '=' ( <FileName> | <Directory> ) <LineEnd>;

<ConnectionDef>           ::= '#CONNECTION' '=' <ConnectionString> <LineEnd>;

<TableNameDef>            ::= '#TABLENAME' '=' ( <TableName> | <FileName> ) <LineEnd>;

/** Type Info:# B = Boolean# Y = Byte# I = Integer# L = Long# U = Currency# S = Single#
    F = Double# D = Date  # C(n) = Text, n = 1..255# O = Long Binary# M = Memo **/
<TTypeInfo>               ::= 'B' | 'Y' | 'I' | 'L' | 'U' | 'S' | 'F' | 'D' |
                              'C' '(' <ColumnWidth> ')' | 'O' | 'M';

//: FieldDefs starting with an asterisk are the Primary Index.
<FieldDef>                ::= [ '*' ] <InputName> ':' <TTypeInfo> [ '=' <OutputName> ] <LineEnd>;

<AssociationDef>          ::= '@' <InputName> '=' <OutputName>;

<TimeLocationDef>         ::= '&' <SymbolName> '=' ( <Rectangle> | <Bar> | <Line> |
                              <Triangle> | <Ellipse> | <Diamond> | <SuperBar> );

<Rectangle>               ::= 'RECTANGLE' ',' <BorderDef> ',' <InteriorDef> [ ',' <Transparency> ] <LineEnd>;

<Bar>                     ::= 'BAR' ',' <BorderDef> ',' <InteriorDef> ',' <BarWidth> [ ',' <Transparency> ] <LineEnd>;

<Line>                    ::= 'LINE' ',' <BorderDef> [ <LineEndDefs> ] <LineEnd>;

<LineEndDefs>             ::= <StartType> ',' <StartSize> ',' <EndType> ',' <EndSize>;

<StartType>               ::= <LineEndType>;

<EndType>                 ::= <LineEndType>;

<StartSize>               ::= <LineEndSize>;

<EndSize>                 ::= <LineEndSize>;

<LineEndType>             ::= 'NONE' | 'DIAMOND' | 'OPEN' | 'OVAL' | 'STEALTH' | 'STYLEMIXED' | 'TRIANGLE';

<LineEndSize>             ::= 'ShortNarrow' | 'MediumNarrow' | 'LongNarrow' |
                              'ShortMedium' | 'MediumMedium' | 'LongMedium' |
                              'ShortWide'   | 'MediumWide'   | 'LongWide';

<Triangle>                ::= 'TRIANGLE' ',' <BorderDef> ',' <InteriorDef> ',' <TriangleType> [ ',' <Transparency> ] <LineEnd>;

<Ellipse>                 ::= 'ELLIPSE' ',' <BorderDef> ',' <InteriorDef> ',' <EllipseRadius> [ ',' <Transparency> ] <LineEnd>;

<Diamond>                 ::= 'DIAMOND' ',' <BorderDef> ',' <InteriorDef> ',' <DiamondSize> [ ',' <Transparency> ] <LineEnd>;

<SuperBar>                ::= 'SUPERBAR' ',' <BorderDef> ',' <InteriorDef> ',' <DateWidth> ',' <LocationWidth> [ ',' <Transparency> ] <LineEnd>;

<BorderDef>               ::= <BorderColour> ',' <BorderLineStyle> ',' <BorderWeight>;

<InteriorDef>             ::= <InteriorColour> ',' <InteriorPattern> ',' <InteriorPatternColour>;

<BorderColour>            ::= <ColourName>;

<BarWidth>                ::= <PositiveSingle>;

<DateWidth>               ::= <PositiveSingle>;

<LocationWidth>           ::= <PositiveSingle>;

<TriangleType>            ::= 'STARTANDEARLY' | 'STARTANDLATE' | 'ENDANDEARLY' | 'ENDANDLATE';

<EllipseRadius>           ::= <PositiveSingle>;

<DiamondSize>             ::= <PositiveSingle>;

<Transparency>            ::= [ ',' <PositiveSingle> ];

<ColourName>              ::= 'BLACK' | 'BROWN' | 'OLIVEGREEN' | 'DARKGREEN' | 'DARKTEAL' | 'DARKBLUE' |
                              'INDIGO' | 'GRAY-80%' | 'DARKRED' | 'ORANGE' | 'DARKYELLOW' | 'GREEN' |
                              'TEAL' | 'BLUE' | 'BLUE-GRAY' | 'GRAY-50%' | 'RED' | 'LIGHTORANGE' |
                              'LIME' | 'SEAGREEN' | 'AQUA' | 'LIGHTBLUE' | 'VIOLET' | 'GRAY-40%' |
                              'PINK' | 'GOLD' | 'YELLOW' | 'BRIGHTGREEN' | 'TURQUOISE' | 'SKYBLUE' |
                              'PLUM' | 'GRAY-25%' | 'ROSE' | 'TAN' | 'LIGHTYELLOW' | 'LIGHTGREEN' |
                              'LIGHTTURQUOISE' | 'PALEBLUE' | 'LAVENDER' | 'WHITE';

<BorderLineStyle>         ::= 'SOLID' | 'ROUNDDOT' | 'SQUAREDOT' | 'DASH' | 'DASHDOT' | 'LONGDASH' |
                              'LONGDASHDOT' | 'DASHDOTDOT';

<BorderWeight>            ::= '0.25' | '0.5' | '1' | '1.5' | '2.25' | '3' | '4.5' | '6' | 'DOUBLE' |
                              'DOUBLETHINTHICK' | 'DOUBLETHICKTHIN' | 'TRIPLETHICKBETWEENTHIN';

<InteriorColour>          ::= <ColourName>;

<InteriorPattern>         ::= 'NONE' | '10PERCENT' | '20PERCENT' | '25PERCENT' | '30PERCENT' | '40PERCENT' |
                              '50PERCENT' | '5PERCENT' | '60PERCENT' | '70PERCENT' | '75PERCENT' |
                              '80PERCENT' | '90PERCENT' | 'DARKDOWNWARDDIAGONAL' | 'DARKHORIZONTAL' |
                              'DARKUPWARDDIAGONAL' | 'DARKVERTICAL' | 'DASHEDDOWNWARDDIAGONAL' |
                              'DASHEDHORIZONTAL' | 'DASHEDUPWARDDIAGONAL' | 'DASHEDVERTICAL' |
                              'DIAGONALBRICK' | 'DIVOT' | 'DOTTEDGRID' | 'HORIZONTALBRICK' |
                              'LARGECHECKERBOARD' | 'LARGECONFETTI' | 'LARGEGRID' | 'LIGHTDOWNWARDDIAGONAL' |
                              'LIGHTHORIZONTAL' | 'LIGHTUPWARDDIAGONAL' | 'LIGHTVERTICAL' | 'NARROWHORIZONTAL' |
                              'NARROWVERTICAL' | 'OUTLINEDDIAMOND' | 'PLAID' | 'SHINGLE' | 'SMALLCHECKERBOARD' |
                              'SMALLCONFETTI' | 'SMALLGRID' | 'SOLIDDIAMOND' | 'SPHERE' | 'TRELLIS' | 'WAVE' |
                              'WEAVE' | 'WIDEDOWNWARDDIAGONAL' | 'WIDEUPWARDDIAGONAL' | 'ZIGZAG';

<InteriorPatternColour>   ::= <ColourName>;

<DefinitionName>          ::= <Name>;

//: Some databases will note allow tables names with spaces and anyother characters outside the 'a'..'z' + '0'..'9'.
<TableName>               ::= <Name>;

<ColumnWidth>             ::= <ByteNumber>;

//: InputName = Excel Worksheet Column Name.
<InputName>               ::= <Name>;

//: OutputName = Text File / Database Field Name.
<OutputName>              ::= <Name>;

<Name>                    ::= ( <Identifier> | <WhiteSpace> | <GeneralSymbols> )*;

<ByteNumber>              ::= ? An integer number between 1 and 255 ?;

<PositiveSingle>          ::= ? A positive single precision number ?;

<LineEnd>                 ::= ? Line feed and or carriage return ?;

<FileName>                ::= ? Drive:\Path\FileName.Extension with no quotes ?;

<Directory>               ::= ? Drive:\Path\ with no quotes ?;

<SymbolName>              ::= ( <Identifier> | <WhiteSpace> )+;

//: MS Aceess = ""; dBASE = "dBASE IV;"; Paradox = "Paradox 4.x;"; FoxPro = "FoxPro 2.6;" and Text = "Text;".
<ConnectionString>        ::= '' | 'dBASE IV;' | 'Paradox 4.x;' | 'FoxPro 2.6;' | 'Text;';

<AlphaCharacters>         ::= ? All alphabeta characters = ['A'..'Z', 'a'..'z', '_'] ?;

<NumericCharacters>       ::= ? All Numeric Characters = ['0'..'9'] ?;

<GeneralSymbols>          ::= ? General Symbols = [#33..#128] - ['=', ':', '*', '(', ')', '#', '&', '@', '{', '}'] ?;

<WhiteSpace>              ::= ? Whitespace = [#32, #9] {Space and Tab} ?;

<Identifier>              ::= <AlphaCharacters> | ( <AlphaCharacters> | <NumericCharacters> )*;
